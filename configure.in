dnl configure.in for the SASL library
dnl Rob Earhart
dnl
dnl
dnl         Copyright 1998 by Carnegie Mellon University
dnl
dnl                       All Rights Reserved
dnl
dnl Permission to use, copy, modify, and distribute this software and its
dnl documentation for any purpose and without fee is hereby granted,
dnl provided that the above copyright notice appear in all copies and that
dnl both that copyright notice and this permission notice appear in
dnl supporting documentation, and that the name of CMU not be
dnl used in advertising or publicity pertaining to distribution of the
dnl software without specific, written prior permission.
dnl
dnl CMU DISCLAIMS ALL WARRANTIES WITH REGARD TO THIS SOFTWARE, INCLUDING
dnl ALL IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS, IN NO EVENT SHALL
dnl CMU BE LIABLE FOR ANY SPECIAL, INDIRECT OR CONSEQUENTIAL DAMAGES OR
dnl ANY DAMAGES WHATSOEVER RESULTING FROM LOSS OF USE, DATA OR PROFITS,
dnl WHETHER IN AN ACTION OF CONTRACT, NEGLIGENCE OR OTHER TORTIOUS ACTION,
dnl ARISING OUT OF OR IN CONNECTION WITH THE USE OR PERFORMANCE OF THIS
dnl SOFTWARE.
dnl
AC_INIT(lib/saslint.h)
AC_CONFIG_AUX_DIR(config)
AM_CONFIG_HEADER(config.h)
AC_CANONICAL_HOST

dnl
dnl REMINDER: When changing the version number here, please also update
dnl the values in win32/include/winconfig.h and config/cyrus-sasl.spec as well.
dnl
AM_INIT_AUTOMAKE(cyrus-sasl, 1.4.2)
CMU_INIT_AUTOMAKE

DIRS=""

dnl With and Enable
AC_ARG_WITH(nana, [  --with-nana             use NANA [no]],,with_nana=no)

AC_ARG_ENABLE(sample, [  --enable-sample         compile sample code [yes]],,enable_sample=yes)

AC_ARG_ENABLE(java, [  --enable-java           compile Java support [no]],,enable_java=no)

AC_PROG_CC
AC_PROG_CPP
AC_PROG_INSTALL

if test "$enable_java" = yes; then
  AC_PATH_PROG(JAVAC, javac, no)
  AC_PATH_PROGS(JAVAH, javah kaffeh, no)
  AC_CHECK_PROGS(JAVADOC, javadoc, :)
  if test "$JAVAC" = "no" -o "$JAVAH" = "no"; then
    AC_WARN([Disabling Java support])
    enable_java=no
  fi
fi

AM_DISABLE_STATIC

CMU_PROG_LIBTOOL

if test "$ac_cv_prog_gcc" = yes; then
  CFLAGS="-Wall -W ${CFLAGS}"
fi

AC_ARG_WITH(purecov,[  --with-purecov          link with purecov])
if test "$with_purecov" = yes; then
  AC_CHECK_PROGS(PURECOV, purecov)
fi
AC_ARG_WITH(purify,[  --with-purify           link with purify])
if test "$with_purify" = yes; then
  AC_CHECK_PROGS(PURIFY, purify)
fi

if test "$with_nana" = yes -a "$ac_cv_prog_gcc" = yes; then
  AC_CHECK_LIB(nana, nana_error)
fi

if test "$enable_java" = yes; then
  AC_MSG_CHECKING([JNI cpp flags])
  if test `echo $JAVAH | sed 's,.*/,,'` = "kaffeh"; then
    JAVA_INCLUDES=-I`echo $JAVAH | sed -e 's,/bin.*,/include/kaffe,'`
  else
    java_base=`echo $JAVAC | sed 's,/bin.*,/lib/java,'`
    JAVA_INCLUDES=''
    for dir in `find ${java_base}/include -follow -type d -print | grep -v green_threads`; do
      JAVA_INCLUDES="${JAVA_INCLUDES} -I$dir"
    done
  fi
  AC_SUBST(JAVA_INCLUDES)
  AC_MSG_RESULT($JAVA_INCLUDES)
  JAVAC=`echo "$JAVAC" | sed 's,.*/,,'`
  JAVAH=`echo "$JAVAH" | sed 's,.*/,,'`
fi

if test "$enable_java" = yes; then
  DIRS="$DIRS sasl"
fi

if test "$enable_sample" = yes; then
  DIRS="$DIRS sample"
fi

dnl Figure out what database path we're using
AC_ARG_WITH(dbpath, [  --with-dbpath=PATH      set the DB path to use [/etc/sasldb] ],
  dbpath=$withval,
  dbpath=/etc/sasldb)
AC_MSG_CHECKING(DB path to use)
AC_MSG_RESULT($dbpath)
AC_DEFINE_UNQUOTED(SASL_DB_PATH, "$dbpath")

dnl Figure out what database type we're using
cmu_save_LIBS="$LIBS"
AC_ARG_WITH(dblib, [  --with-dblib=DBLIB      set the DB library to use [gdbm] ],
  dblib=$withval,
  dblib=auto_detect)
SASL_DB_LIB=""
case "$dblib" in
  gdbm)
	AC_CHECK_HEADER(gdbm.h,
			AC_CHECK_LIB(gdbm, gdbm_open,SASL_DB_LIB="-lgdbm",dblib=no),
			dblib=no)
	;;
  ndbm)
	dnl We want to attempt to use -lndbm if we can, just in case
	dnl there's some version of it installed and overriding libc
	AC_CHECK_HEADER(ndbm.h,
			AC_CHECK_LIB(ndbm, dbm_open,SASL_DB_LIB="-lndbm",
				AC_CHECK_FUNC(dbm_open,,dblib=no)),
			dblib=no)
	;;
  auto_detect)
	dnl Can we use gdbm?
	AC_CHECK_HEADER(gdbm.h,
			AC_CHECK_LIB(gdbm, gdbm_open,dblib=gdbm;SASL_DB_LIB="-lgdbm",dblib=no),
			dblib=no)
	if test "$dblib" = no; then
		dnl How about ndbm?
		AC_CHECK_HEADER(ndbm.h,
				AC_CHECK_LIB(ndbm, dbm_open,
					     dblib=ndbm;SASL_DB_LIB="-lndbm",
					AC_CHECK_FUNC(dbm_open,
						      dblib=ndbm, dblib=no)),
				dblib=no)
	fi
	;;
  *)
	AC_MSG_WARN([Bad DB library implementation specified;])
	AC_MSG_WARN([Use either "gdbm" or "ndbm"])
	dblib=no
	;;
esac
LIBS="$cmu_save_LIBS"
AC_MSG_CHECKING(DB library to use)
AC_MSG_RESULT($dblib)
if test "$dblib" = "no"; then
  AC_MSG_WARN([Disabling SASL authentication database support])
  SASL_DB_BACKEND="db_none.lo"
  SASL_DB_LIB=""
else
  SASL_DB_BACKEND="db_${dblib}.lo"
fi
AC_SUBST(SASL_DB_BACKEND)
AC_SUBST(SASL_DB_LIB)

AC_CHECK_LIB(dl, dlopen, SASL_DL_LIB="-ldl", SASL_DL_LIB="")
AC_SUBST(SASL_DL_LIB)

dnl See if we can provide a default logging function...
AC_CHECK_FUNCS(vsyslog)

LIB_CRYPT=""
AC_CHECK_FUNC(crypt, cmu_have_crypt=yes,
  AC_CHECK_LIB(crypt, crypt,
	       LIB_CRYPT="-lcrypt"; cmu_have_crypt=yes,
	       cmu_have_crypt=no))

CMU_SOCKETS

dnl CRAM-MD5
AC_ARG_ENABLE(cram, [  --enable-cram           enable CRAM-MD5 authentication [yes] ],
  cram=$enableval,
  cram=yes)

AC_MSG_CHECKING(CRAM-MD5)
if test "$cram" != no; then
  AC_MSG_RESULT(enabled)
  SASL_MECHS="$SASL_MECHS libcrammd5.la"
else
  AC_MSG_RESULT(disabled)
fi


dnl SCRAM-MD5
AC_ARG_ENABLE(scram, [  --enable-scram          enable SCRAM-MD5 authentication [no] ],
  scram=$enableval,
  scram=no)

AC_MSG_CHECKING(SCRAM-MD5)
if test "$scram" != no; then
  AC_MSG_RESULT(enabled)
  SASL_MECHS="$SASL_MECHS libscrammd5.la"
else
  AC_MSG_RESULT(disabled)
fi


dnl DIGEST-MD5
AC_ARG_ENABLE(digest, [  --enable-digest         enable DIGEST-MD5 authentication [yes] ],
  digest=$enableval,
  digest=yes)

if test "$digest" != no; then
  dnl In order to compile digest, we need libdes.
  if test -d $digest; then
    CPPFLAGS="$CPPFLAGS -I$digest/include"
    LDFLAGS="$LDFLAGS -L$digest/lib"
  fi
  AC_CHECK_LIB(des, des_pcbc_encrypt,:,
	       AC_WARN(Disabling DIGEST-MD5); digest=no)
fi

AC_MSG_CHECKING(DIGEST-MD5)
if test "$digest" != no; then
  AC_MSG_RESULT(enabled)
  SASL_MECHS="$SASL_MECHS libdigestmd5.la"
else
  AC_MSG_RESULT(disabled)
fi


dnl KERBEROS_V4
AC_ARG_ENABLE(krb4, [  --enable-krb4           enable KERBEROS_V4 authentication [yes] ],
  krb4=$enableval,
  krb4=yes)

if test "$krb4" != no; then
  dnl In order to compile kerberos4, we need libkrb and libdes.
  if test -d ${krb4}; then
     CPPFLAGS="$CPPFLAGS -I$krb4/include"
     LDFLAGS="$LDFLAGS -L$krb4/lib"
  fi
  AC_CHECK_LIB(des, des_pcbc_encrypt,
	AC_CHECK_LIB(krb, krb_mk_priv,:,
		     AC_WARN(Disabling KERBEROS_V4); krb4=no,
		     -ldes)
  , AC_WARN(Disabling KERBEROS_V4); krb4=no)
fi

AC_MSG_CHECKING(KERBEROS_V4)
if test "$krb4" != no; then
  AC_MSG_RESULT(enabled)
  SASL_MECHS="$SASL_MECHS libkerberos4.la"
else
  AC_MSG_RESULT(disabled)
fi

dnl GSSAPI
AC_ARG_ENABLE(gssapi, [  --enable-gssapi	  enable GSSAPI authentication [yes] ],
  gssapi=$enableval,
  gssapi=yes)

if test "$gssapi" != no; then
  if test -d ${gssapi}; then
     CPPFLAGS="$CPPFLAGS -I$gssapi/include"
     LDFLAGS="$LDFLAGS -L$gssapi/lib"
  fi
  AC_CHECK_HEADER(gssapi.h,,AC_WARN(Disabling GSSAPI); gssapi=no)
fi

if test "$gssapi" != no; then
  dnl We need to find out which gssapi implementation we are
  dnl using. Supported alternatives are: MIT Kerberos 5 and
  dnl Heimdal Kerberos 5 (http://www.pdc.kth.se/heimdal)
  dnl
  dnl The choice is reflected in GSSAPIBASE_LIBS
  gss_impl="mit";
  AC_CHECK_LIB(resolv,res_search)
  if test -d ${gssapi}; then 
     CPPFLAGS="$CPPFLAGS -I$gssapi/include"
     LDFLAGS="$LDFLAGS -L$gssapi/lib"
  fi

  AC_CHECK_LIB(roken,base64_decode,gss_impl="heimdal", $LIB_CRYPT)

  if test -d ${gssapi}; then
     GSSAPIBASE_LIBS="-L$gssapi"
  fi

  if test "$gss_impl" = mit; then
     GSSAPIBASE_LIBS="$GSSAPIBASE_LIBS/lib -lgssapi_krb5 -lkrb5 -lcrypto -lcom_err"
  elif test "$gss_impl" = "heimdal"; then
     GSSAPIBASE_LIBS="$GSSAPIBASE_LIBS/lib -lgssapi -lkrb5 -ldes -lasn1 -lroken ${LIB_CRYPT} -lcom_err"
  else
     gssapi="no"
     AC_WARN(Disabling GSSAPI)
  fi

fi

GSSAPI_LIBS=""
AC_MSG_CHECKING(GSSAPI)
if test "$gssapi" != no; then
  AC_MSG_RESULT(with implementation ${gss_impl})
  AC_CHECK_LIB(ndbm,dbm_open,GSSAPIBASE_LIBS="$GSSAPIBASE_LIBS -lndbm")
  AC_CHECK_LIB(resolv,res_search,GSSAPIBASE_LIBS="$GSSAPIBASE_LIBS -lresolv")
  SASL_MECHS="$SASL_MECHS libgssapiv2.la"
else
  AC_MSG_RESULT(disabled)
fi
AC_SUBST(GSSAPI_LIBS)
AC_SUBST(GSSAPIBASE_LIBS)

dnl ANONYMOUS
AC_ARG_ENABLE(anon, [  --enable-anon           enable ANONYMOUS authentication [yes] ],
  anon=$enableval,
  anon=yes)

AC_MSG_CHECKING(ANONYMOUS)
if test "$anon" != no; then
  AC_MSG_RESULT(enabled)
  SASL_MECHS="$SASL_MECHS libanonymous.la"
else
  AC_MSG_RESULT(disabled)
fi


dnl PLAIN
AC_ARG_ENABLE(plain, [  --enable-plain          enable PLAIN authentication [yes] ],
  plain=$enableval,
  plain=yes)

PLAIN_LIBS=""
if test "$plain" != no; then
  dnl In order to compile plain, we need crypt.
  if test "$cmu_have_crypt" = yes; then
    PLAIN_LIBS=$LIB_CRYPT
  else
    AC_WARN(Disabling PLAIN (no crypt))
    plain=no
  fi
fi
AC_SUBST(PLAIN_LIBS)

AC_MSG_CHECKING(PLAIN)
if test "$plain" != no; then
  AC_MSG_RESULT(enabled)
  SASL_MECHS="$SASL_MECHS libplain.la"
else
  AC_MSG_RESULT(disabled)
fi

AC_SUBST(SASL_MECHS)


AC_ARG_WITH(plugindir, [  --with-plugindir=DIR    set the directory where plugins will
                          be found [/usr/lib/sasl] ],
  plugindir=$withval,
  plugindir=/usr/lib/sasl)
AC_DEFINE_UNQUOTED(PLUGINDIR, "$plugindir")
AC_SUBST(plugindir)

AC_ARG_WITH(rc4, [  --with-rc4=DIR          use rc4 (look in DIR) [yes] ],
	with_rc4=$withval,
	with_rc4=yes)
if test "$with_rc4" != no; then
  if test -d $with_rc4; then
    CPPFLAGS="$CPPFLAGS -I${with_rc4}/include"
    LDFLAGS="$LDFLAGS -I${with_rc4}/lib"
  fi
  cmu_save_LIBS="$LIBS"
  AC_CHECK_LIB(rc4, rc4_init,
	  AC_CHECK_HEADER(rc4.h,,
		          AC_WARN([Disabling rc4 support]); with_rc4=no),
	       AC_WARN([Disabling rc4 support]); with_rc4=no)
  LIBS="$cmu_save_LIBS"
fi

AC_MSG_CHECKING(rc4 support)
AC_MSG_RESULT($with_rc4)
LIB_RC4=""
if test "$with_rc4" != no; then
  AC_DEFINE(WITH_RC4)
  LIB_RC4="-lrc4"
fi
AC_SUBST(LIB_RC4)

AC_HEADER_DIRENT
AC_HEADER_STDC
AC_CHECK_FUNCS(strchr memcpy getspnam)
AC_CHECK_HEADERS(getopt.h unistd.h crypt.h shadow.h)
AC_C_CONST
AC_C_INLINE

AC_CHECK_HEADER(sfio.h,
	        SASL_UTIL_LIBS_EXTRA=libsfsasl.la
	          SASL_UTIL_HEADERS_EXTRA=sfsasl.h,
                SASL_UTIL_LIBS_EXTRA=""
                  SASL_UTIL_HEADERS_EXTRA="")
AC_SUBST(SASL_UTIL_LIBS_EXTRA)
AC_SUBST(SASL_UTIL_HEADERS_EXTRA)

AC_CHECK_FUNCS(getdomainname getpassphrase)

AC_SUBST(DIRS)

AC_OUTPUT(Makefile include/Makefile lib/Makefile plugins/Makefile utils/Makefile doc/Makefile sample/Makefile man/Makefile)
